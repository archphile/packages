# Maintainer Tux <info@archphile.org>
# Based on ffmpeg-headless from AUR
# $Id: PKGBUILD 228056 2014-12-26 11:57:24Z bpiotrowski $
# Maintainer:  Ionut Biru <ibiru@archlinux.org>
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Paul Mattal <paul@archlinux.org>

pkgname=ffmpeg-archphile
pkgver=2.5.1
pkgrel=1
epoch=1
pkgdesc='The FFmpeg media toolkit optimized for server (headless) systems'
arch=('armv6h' 'armv7h')
url='http://ffmpeg.org/'
license=('GPL')
provides=("ffmpeg=$pkgver-$pkgrel")
conflicts=("ffmpeg")
replaces=("ffmpeg")
depends=(
      'bzip2' 'gnutls' 'lame'
      'libmodplug' 'libtheora' 'libvorbis'
      'opencore-amr' 'rtmpdump'
      'zlib' 'libfdk-aac'
)
makedepends=('yasm' 'hardening-wrapper')
source=(http://ffmpeg.org/releases/ffmpeg-$pkgver.tar.bz2)
md5sums=('ad3b4c189e9a7d43ae54c52d409a43ef')

build() {
  cd ffmpeg-$pkgver

  ./configure \
    --prefix=/usr \
    --disable-debug \
    --disable-static \
    --disable-stripping \
    --enable-avresample \
    --enable-gnutls \
    --enable-gpl \
    --enable-libmodplug \
    --enable-libmp3lame \
    --enable-libopencore_amrnb \
    --enable-libopencore_amrwb \
    --enable-librtmp \
    --enable-libtheora \
    --enable-libvorbis \
    --enable-runtime-cpudetect \
    --enable-shared \
    --enable-swresample \
    --enable-version3 \
    --enable-libfdk_aac \
    --enable-nonfree

  make
  make doc/ff{mpeg,server}.1
}

package() {
  cd ffmpeg-$pkgver
  make DESTDIR="$pkgdir" install install-man
}
