# Maintainer Tux <info@archphile.org>
# Based on ffmpeg-headless from AUR
# $Id: PKGBUILD 228056 2014-12-26 11:57:24Z bpiotrowski $
# Maintainer:  Ionut Biru <ibiru@archlinux.org>
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Paul Mattal <paul@archlinux.org>

pkgname=ffmpeg-archphile
pkgver=2.8.6
pkgrel=1
epoch=1
pkgdesc='The FFmpeg media toolkit optimized for server (headless) systems'
arch=('arm' 'armv6h' 'armv7h')
url='http://ffmpeg.org/'
license=('GPL')
provides=("ffmpeg=$pkgver-$pkgrel")
conflicts=("ffmpeg")
replaces=("ffmpeg")
depends=(
      'bzip2' 'gnutls' 'lame'
      'libmodplug' 'libtheora' 'libvorbis'
      'opencore-amr' 'rtmpdump'
      'zlib' 'libfdk-aac'
)
makedepends=('yasm' 'hardening-wrapper')
source=(http://ffmpeg.org/releases/ffmpeg-$pkgver.tar.bz2)
sha1sums=('74f82c0bdaada050ada9010ecb574fb02d434c00')

build() {
  cd ffmpeg-$pkgver

  ./configure \
    --prefix=/usr \
    --disable-debug \
    --disable-static \
    --disable-stripping \
    --disable-avdevice \
    --disable-avfilter \
    --disable-swscale \
    --disable-ffplay \
    --disable-ffserver \
    --disable-network \
    --disable-muxers \
    --disable-demuxers \
    --disable-bsfs \
    --disable-filters \
    --disable-parsers \
    --disable-indevs \
    --disable-outdevs \
    --disable-encoders \
    --enable-avresample \
    --enable-gnutls \
    --enable-gpl \
    --enable-libmodplug \
    --enable-libmp3lame \
    --enable-libopencore_amrnb \
    --enable-libopencore_amrwb \
    --enable-librtmp \
    --enable-libtheora \
    --enable-libvorbis \
    --enable-runtime-cpudetect \
    --enable-shared \
    --enable-swresample \
    --enable-version3 \
    --enable-libfdk_aac \
    --enable-nonfree

  make
  make doc/ff{mpeg,server}.1
}

package() {
  cd ffmpeg-$pkgver
  make DESTDIR="$pkgdir" install install-man
}
