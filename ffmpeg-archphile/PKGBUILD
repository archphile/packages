# Maintainer: Tux <info@archphile.org>
# Website: http://archphile.org
# Based on ffmpeg-headless from AUR made by the following people
# $Id: PKGBUILD 228056 2014-12-26 11:57:24Z bpiotrowski $
# Maintainer:  Ionut Biru <ibiru@archlinux.org>
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Paul Mattal <paul@archlinux.org>


_pkgname=ffmpeg
pkgname=ffmpeg-archphile
pkgver=3.4.1
pkgrel=1
epoch=1
pkgdesc='The FFmpeg media toolkit optimized for server (headless) systems'
arch=('armv7h' 'aarch64')
url='http://ffmpeg.org/'
license=('GPL')
provides=('libavcodec.so' 'libavformat.so'
          'libavresample.so' 'libavutil.so' 'libpostproc.so' 'libswresample.so'
          "ffmpeg=${pkgver}")
conflicts=("ffmpeg")
replaces=("ffmpeg")
depends=(
      'bzip2' 'gnutls' 'lame'
      'libmodplug' 'libtheora' 'libvorbis'
      'opencore-amr' 'rtmpdump'
      'zlib' 'libfdk-aac' 'libvorbis.so'
)
makedepends=('yasm')
source=('http://ffmpeg.org/releases/ffmpeg-$pkgver.tar.bz2'
	'fs56089.patch'
	)
sha256sums=('5a77278a63741efa74e26bf197b9bb09ac6381b9757391b922407210f0f991c0'
            '0bfcd12d1992903f21c146ae56d9ad89b52818cfb2303197ee905347c25a5427')

prepare() {
  cd ffmpeg-${pkgver}


# https://bugs.archlinux.org/task/56089
  # Backport of http://git.videolan.org/?p=ffmpeg.git;a=commitdiff;h=a606f27f4c610708fa96e35eed7b7537d3d8f712
  patch -Np1 -i ../fs56089.patch
}


build() {
  
  cd ffmpeg-$pkgver

  [[ $CARCH == "armv7h" || $CARCH == "aarch64" ]] && CONFIG='--host-cflags="-fPIC"'

  ./configure \
    --prefix='/usr' \
    --disable-debug \
    --disable-static \
    --disable-stripping \
    --disable-avdevice \
    --disable-avfilter \
    --disable-swscale \
    --disable-ffplay \
    --disable-ffserver \
    --disable-network \
    --disable-bsfs \
    --disable-filters \
    --disable-parsers \
    --disable-indevs \
    --disable-outdevs \
    --disable-encoders \
    --enable-decoders \
    --enable-avresample \
    --enable-gnutls \
    --enable-gpl \
    --enable-libmodplug \
    --enable-libmp3lame \
    --enable-libopencore_amrnb \
    --enable-libopencore_amrwb \
    --enable-librtmp \
    --enable-libtheora \
    --enable-libvorbis \
    --enable-runtime-cpudetect \
    --enable-shared \
    --enable-swresample \
    --enable-version3 \
    --enable-libfdk_aac \
    --enable-nonfree

  make
  make doc/ff{mpeg,server}.1
}

package() {
  cd ffmpeg-$pkgver
  make DESTDIR="$pkgdir" install install-man
}
