# Maintainer: Tux <info@archphile.org>
# Website: http://archphile.org
# Based on ympd-git from AUR made by the following people:
# Maintainer: Philippe Mongeau <ph.mongeau@gmail.com>

pkgname=ympd-archphile
_pkgname=ympd
pkgver=1.2.3.r55.g4295d15
pkgrel=2
pkgdesc="A standalone MPD Web GUI - Default port set to 80"
arch=('armv7h' 'aarch64')
url="http://www.ympd.org"
license=('BSD')
depends=('libmpdclient' 'openssl')
makedepends=('cmake' 'git')
optdepends=()
provides=("${_pkgname}")
conflicts=("${_pkgname}")
replaces=('ympd-git')
source=("git+https://github.com/notandy/ympd.git")

sha1sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  git describe --long --tags | sed -E 's/^v//; s/([^-]*-g)/r\1/; s/-/./g'
}

build() {
  cd "${srcdir}/${_pkgname}"

#  msg2 "Applying various patches"
#  patch -Np1 -i ../dirble_api_key.patch
  
  mkdir build && cd build
  cmake .. -DCMAKE_INSTALL_PREFIX:PATH=/usr
  make
}

package() {
  cd "${srcdir}/${_pkgname}/build"
  make DESTDIR="$pkgdir/" install
  install -Dm644  "${srcdir}/${_pkgname}/contrib/ympd.service" "$pkgdir/usr/lib/systemd/system/ympd.service"
  install -Dm644  "${srcdir}/${_pkgname}/contrib/ympd.default" "$pkgdir/etc/default/ympd"
  sed '/WEB_PORT=8080/c WEB_PORT=80' -i "$pkgdir/etc/default/ympd"
}

