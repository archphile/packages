# Maintainer: Mike Andonov <info@archphile.org>
# Website: http://archphile.org
# Based on a package made by the following people:
# Maintainer: Anatol Pomozov anatol dot pomozov at g mail
# Contributor: Leslie P. Polzer <polzer@gnu.org>
# Contributor: bender02 at archlinux dot us
# Contributor: Felix Saparelli <me@passcod.name>

pkgname=mongoose
pkgver=5.5
pkgrel=7
pkgdesc='Easy to use, powerful, embeddable web server'
arch=('armv7h' 'aarch64')
license=(GPL2)
depends=(glibc openssl)
backup=(etc/mongoose/mongoose.conf)
url='https://github.com/cesanta/mongoose'
source=(https://github.com/cesanta/mongoose/archive/$pkgver.tar.gz
        mongoose.conf
        mongoose.service)
sha256sums=('2ea925667e2e8ac09c649fb4848c1702247565a9006da2a7f2b2ba378f4cd534'
            'abd47aec59ee0b6993a8311a6e7257fe6c0ffad0f30a87989333f89cf32596ad'
            'd6148e729e43bd8dee2e18798b80d131255ed11953fa2642827f4378e7ba80c6')

build() {
  cd mongoose-$pkgver
  # hopefully upstream will provdide an official Makefile
  # https://github.com/cesanta/mongoose/issues/326
  gcc -shared -fPIC -DNS_ENABLE_SSL $CFLAGS $LDFLAGS mongoose.c -o libmongoose.so -lssl

  cd examples/web_server
  make web_server
}

package() {
  install -D -m644 mongoose.conf "$pkgdir/etc/mongoose/mongoose.conf"
  install -D -m644 mongoose.service "$pkgdir/usr/lib/systemd/system/mongoose.service"

  cd mongoose-$pkgver
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -D -m755 libmongoose.so "$pkgdir/usr/lib/libmongoose.so"
  install -D -m644 mongoose.h "$pkgdir/usr/include/mongoose.h"
  install -D -m755 examples/web_server/web_server "$pkgdir/usr/bin/mongoose"
}
