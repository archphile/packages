# Maintainer: Tux <info@archphile.org>
# Website: http://archphile.org
# Based on squeezelite-git from AUR made by the following people:
#
# Maintainer: Fabio 'Lolix' Loli <lolix@disroot.org> -> https://github.com/FabioLolix
# Contributor: Christian Hesse <mail@eworm.de
# Contributor: Mika Fischer <mika.fischer@zoopnet.de>

pkgname=squeezelite-archphile
_pkgname=squeezelite
_commit=b265c78e583b2c45d6f3ae9d6168776c4da0347e
pkgver=1.9.2.1165
pkgrel=5
pkgdesc='Lightweight headless squeezebox emulator'
arch=('armv7h' 'aarch64')
url='https://github.com/ralph-irving/squeezelite'
license=(GPL3)
makedepends=(git)
depends=(alsa-lib faad2 flac libmad libvorbis mpg123 libsoxr ffmpeg-archphile)
provides=('squeezelite')
conflicts=('squeezelite')
install=squeezelite-archphile.install
source=("${pkgname}-${pkgver}-${_commit}.tar.gz::https://github.com/ralph-irving/squeezelite/archive/${_commit}.tar.gz"
	'service'
	'conffile')
sha256sums=('3f27ac8ecd463a71181f5b788beab7bd70994d1c06c4a811a02fcf5e212a7109'
            '5b39e9754b6bcf06bcaaecab76ebf7c997966160b48692461d3be5d94ee5f004'
            '3ca6728131edb5cd619f6ba7266242df8384bc667bddf11d67c16af7111c7d59')

build() {
  cd "${_pkgname}-${_commit}"

  export LDFLAGS="${LDFLAGS} -lasound -lpthread -lm -lrt"
  export OPTS="${OPTS} -DDSD -DRESAMPLE -DVISEXPORT -DFFMPEG -DLINKALL"
  make
}

package() {
  cd "${_pkgname}-${_commit}"

  install -m0755 -D squeezelite "${pkgdir}/usr/bin/squeezelite"
  install -Dm644 ../conffile "${pkgdir}/etc/squeezelite.conf.default"
  install -Dm644 ../service "${pkgdir}/usr/lib/systemd/system/squeezelite.service"
  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/squeezelite.LICENSE
}



